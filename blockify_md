#!/usr/bin/env perl
use v5.36;
#use open ':std', ':encoding(UTF-8)';
use Data::Printer;
use Getopt::Long::Descriptive;
use Path::Tiny;
use IO::File;

my ($opt, $usage) = describe_options(
    'Usage: blockify FILE',
    ['file|f=s', 'File to blockify', {required => 1}],
    ['force',    'Overwrites file, otherwise print result to STDOUT', {default => 0}],
    ['help|h',   'Print this message', {shortcircuit => 1}],
);

print($usage->text) && exit if $opt->help;

# Check if user provided an existing file
my $file = path($opt->file);
die(sprintf("Error: file '%s' does not exist.\n", $opt->file)) if not $file->exists;
die(sprintf("Error: '%s' is not a file.\n", $opt->file))       if not $file->is_file;

# read file into memory
my $content = $file->slurp;

# $fh is either the file to be overwritten, or otherwise by default STDOUT
my $fh =
    $opt->force 
    ? IO::File->new($file, 'w')
    : IO::Handle->new_from_fd(fileno(STDOUT), 'w');

# Go through file content up to end -- the order of the matching matters!
while ( $content !~ m/\G\z/gxmsc ) {
    # preserve 2 spaces followed by newline
    if ( $content =~ m/\G( [ ][ ]\n )/gxmsc ) {
        $fh->print($1);
    }
    # preserve any not newline characters
    elsif ( $content =~ m/\G( [^\n] )/gxmsc ) {
        $fh->print($1);
    }
    # squash multiple newlines into two newlines
    elsif ( $content =~ m/\G \n{2,} /gxmsc ) {
        $fh->print("\n\n");
    }
    # preserve enumerations
    elsif ( $content =~ m/\G( \n \d+ \. )/gxmsc ) {
        $fh->print($1);
    }
    # preserve lists
    elsif ( $content =~ m/\G( \n \s* [*] )/gxmsc ) {
        $fh->print($1);
    }
    # preserve indented blocks -- by spaces or tab
    elsif ( $content =~ m/\G( \n [ \t] )/gxmsc ) {
        $fh->print($1);
    }
    # multiline quotes into a single-line quote
    elsif ( $content =~ m/\G \n > \s* /gxmsc ) {
        $fh->print(" ");
    }
    # replace newline with space character when followed by non-whitespace character
    elsif ( $content =~ m/\G \n (.)/gxmsc ) {
        $fh->print(" ", $1);
    }
    # match newline at EOF
    elsif ( $content =~ m/\G \n\z /gxmsc ) {
        $fh->print("\n");
    }
    # BOOM!
    else {
        die "Error: Some unexpected error happened. Parsing is not complete, code needs review!\n";
    }
}

# Explicit close -- is done anyway but explicit is better, good behaviour
$fh->close();
