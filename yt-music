#!/usr/bin/env perl
use v5.32;
use warnings;
use feature 'signatures';
no warnings 'experimental::signatures';
use FindBin;
use lib "$FindBin::RealBin";
use Color qw(red cyan);
use IPC::Run qw(run);
use Data::Dump qw(dd);
use Carp qw(croak);
use Getopt::Long;
use JSON qw(decode_json);
use URI;
use URI::QueryParam;

## Configuration
# User Agent to Use
my $ua = "Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:96.0) Gecko/20100101 Firefox/96.0";

# Path to youtube-dl
my $yt = "yt-dlp";

##
# CLI Options
my ($name, $mp3, $ogg) = ("", 0, 1);
GetOptions(
    "n|name=s" => \$name,
    "m|mp3"    => \$mp3,
    "o|ogg"    => \$ogg,
);

# Get URL Parameter
my $uri = URI->new($ARGV[0]) or die "Passed URL not valid.\n";
die "Only http(s) is supported" if not ($uri->scheme eq 'http' || $uri->scheme eq 'https');

# Only download a single file, not list, if --name parameter was passed
if ( $name ) {
    $uri = uri_params_delete_except($uri, 'v');
}


##
# Download file
my ($in, $out, $err);
run [$yt, '--user-agent', $ua, qw(--quiet --continue --ignore-errors --id -f bestaudio --print-json), $uri],
    \$in, \$out, \$err;

# read Json
my $json   = decode_json $out;
my $source = $json->{_filename};
my $info   = getTargetInformation($json, $name, $mp3, $ogg);

# Convert file to audio
if ( $info->{target} eq "mp3" ) {
    system('ffmpeg', '-hide_banner', '-i', $source, '-q:a', 2, '--', $info->{filename});
}
else {
    system('ffmpeg', '-hide_banner', '-i', $source, '-q:a', 8, '--', $info->{filename});
}

unlink $source;
print red("Created: "), cyan($info->{filename}), "\n";


sub getTargetInformation ($json, $targetFilename, $mp3Flag, $oggFlag) {
    my $target = $mp3Flag ? "mp3" : "ogg";

    if ( $targetFilename ) {
        return {
            target   => $target,
            filename => $targetFilename . ($mp3Flag ? ".mp3" : ".ogg"),
        };
    }
    else {
        my $dest = $json->{_filename} =~ s/\.[^.]+\z/.$target/r;
        return {
            target   => $target,
            filename => $dest,
        };
    }
}

sub uri_params_delete_except ($u, @except) {
    my $uri = $u->clone;

    my %keep = map { $_ => 1 } @except;
    for my $key ( $uri->query_param ) {
        $uri->query_param_delete($key) if not $keep{$key};
    }

    return $uri;
}
